plugins {
	id 'org.ajoberstar.grgit' version '2.0.1'
	id 'org.ajoberstar.reckon' version '0.2.1' apply false
}

import org.ajoberstar.reckon.core.Reckoner
import org.ajoberstar.reckon.core.git.GitInventorySupplier
import org.ajoberstar.reckon.core.strategy.*
import org.eclipse.jgit.lib.Repository
import java.util.Optional

group 'org.uulib.gradletag'

wrapper {
	gradleVersion = '4.2'
}

File subprojectsGradle = file('gradle/subprojects.gradle')
Repository repo = grgit.repository.jgit.repository
Set<String> stages = ['alpha', 'beta', 'final']

subprojects {
	apply from: subprojectsGradle
	
	status = project.properties["${project.name}.status"] ?: 'dev'
	
	version = Reckoner.reckon(
			new GitInventorySupplier(repo, {Optional.ofNullable(it.replaceAll("^${project.name}-", ''))}),
			new ScopeNormalStrategy({Optional.ofNullable(project.properties["${project.name}.scope"])}),
			new StagePreReleaseStrategy(stages, {project.status=='dev' ? Optional.empty() : Optional.of(project.status)})
	)
	
	apply plugin: 'org.uulib.gittag'
	
	gradletag {
		tags {
			versionTag {
				tag = "${project.name}-${project.version}"
			}
		}
	}
	
	pluginManager.withPlugin('com.gradle.plugin-publish') {
		pluginBundle {
			website = 'https://github.com/hWorblehat/gradletag'
			vcsUrl = 'https://github.com/hWorblehat/gradletag'
		}
		
		tagVcsWithVersionTag.mustRunAfter publishPlugins
		
		task('releasePlugins') {
			group = "Publishing"
			description = "Publishes Gradle plugins to the plugin portal, and tags git with the version number"
			dependsOn 'publishPlugins', 'tagVcsWithVersionTag'
		}
	}
}
