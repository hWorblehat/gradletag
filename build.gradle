plugins {
	id 'org.ajoberstar.grgit'
}

group 'org.uulib.gradletag'

wrapper {
	gradleVersion = '4.3'
}

File subprojectsGradle = file('gradle/subprojects.gradle')

subprojects {
	apply from: subprojectsGradle
	
	apply plugin: 'org.uulib.gittag'
	
	gradletag {
		tags {
			versionTag {
				tag = "${project.name}-${project.version}"
			}
		}
	}
	
	pluginManager.withPlugin('com.gradle.plugin-publish') {
		pluginBundle {
			website = 'https://github.com/hWorblehat/gradletag'
			vcsUrl = 'https://github.com/hWorblehat/gradletag'
			
			mavenCoordinates {
				artifactId = project.moduleName
			}
		}
		
		tagVcsWithVersionTag.mustRunAfter publishPlugins
		
		task('releasePlugins') {
			group = "Publishing"
			description = "Publishes Gradle plugins to the plugin portal, and tags git with the version number"
			dependsOn 'publishPlugins', 'tagVcsWithVersionTag'
		}
	}
}
